apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.azure.android'
def CALLING_LIB_ARTIFACT_ID = 'azure-communication-ui-calling'
def CALLING_LIB_VERSION = "$call_library_version_name"
def CALLING_LIB_DESCRIPTION = "Azure Communication Calling UI for Android."

Properties localProps = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    localProps.load(project.rootProject.file('local.properties').newDataInputStream())
}

publishing {
    repositories {
        maven {
            name = 'privateGitHubRepo'
            url = localProps.getProperty('GITHUB_MAVEN_REPO_URL', '""')
            credentials {
                username localProps.getProperty('GITHUB_MAVEN_REPO_USERNAME', '""')
                password localProps.getProperty('GITHUB_MAVEN_REPO_PASSWORD', '""')
            }
        }
        maven {
            name = 'localDirectory'
            url = "file://${buildDir}/publish"
        }
    }

    publications {
        release(MavenPublication) {
            pom {
                name = "${LIB_GROUP_ID}:${CALLING_LIB_ARTIFACT_ID}"
                description = CALLING_LIB_DESCRIPTION
                url = "https://github.com/Azure/communication-ui-library-android"

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/Azure/communication-ui-library-android/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'Microsoft'
                        name = 'Microsoft'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Azure/communication-ui-library-android.git'
                    developerConnection = 'scm:git:ssh:github.com:Azure/communication-ui-library-android.git'
                    url = 'https://github.com/Azure/communication-ui-library-android'
                }
            }
            groupId LIB_GROUP_ID
            artifactId CALLING_LIB_ARTIFACT_ID
            version CALLING_LIB_VERSION

            afterEvaluate {
                from components.release
            }
        }

        callingSDK(MavenPublication) {
            pom {
                name = "Microsoft Azure Android Voice &amp; Video Calling Library for Communication Services"
                description = "This package contains Android client library code for enabling Voice and Video Calling capabilities for Azure Communication Services"
                url = "https://github.com/Azure/azure-sdk-for-android"
                licenses {
                    license {
                        name = 'Licensed under the Microsoft Software License Terms for the Azure Communications Services, Azure CPaaS, AZURE COMMUNICATION SERVICES VOICE AND VIDEO CALLING CLIENT LIBRARY. See EULA.txt for license information.'
                        url = 'https://repo.maven.apache.org/maven2/com/azure/android/azure-communication-calling/1.0.0-beta.1/azure-communication-calling-1.0.0-beta.1-eula.md'
                    }
                }
                developers {
                    developer {
                        id = 'Microsoft'
                        name = 'Microsoft'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Azure/communication-ui-library-android.git'
                    developerConnection = 'scm:git:ssh:github.com:Azure/communication-ui-library-android.git'
                    url = 'https://github.com/Azure/communication-ui-library-android'
                }
            }

            //groupId 'com.azure.android'
            //artifactId 'azure-communication-calling'
            //version '2.10.0'

            //artifact("C:\\Skype2\\SpoolCallingStack\\native\\staging.d\\release\\BETA\\sdk\\arm64-v8a\\azure-communication-calling-1.0.0-beta.1.aar")

            //groupId 'com.microsoft'
            //artifactId 'trouter-client-android'
            //version '2.0.0'

            //artifact("C:\\Skype2\\SpoolCallingStack\\native\\staging.d\\release\\BETA\\sdk\\arm64-v8a\\trouter-client-android.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.azure.android')
                dependencyNode.appendNode('artifactId', 'azure-communication-common')
                dependencyNode.appendNode('version', '1.1.0')

                dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'net.sourceforge.streamsupport')
                dependencyNode.appendNode('artifactId', 'android-retrofuture')
                dependencyNode.appendNode('version', '1.7.3')

                dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.google.code.gson')
                dependencyNode.appendNode('artifactId', 'gson')
                dependencyNode.appendNode('version', '2.10')

            }
        }
    }
}
