plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply from: file('publish.gradle')
apply from: file('jacoco.gradle')
apply from: file('../java_compile_flags.gradle')

// Compile time features
def FEATURE_SUPPORT_FILES = "SupportFiles"
def AVAILABLE_FEATURES = [FEATURE_SUPPORT_FILES]
def BETA_FEATURES = [FEATURE_SUPPORT_FILES]
def GA_FEATURES = []
def ENABLED_FEATURES = []
if (isUsingBetaSdk) {
    ENABLED_FEATURES = BETA_FEATURES
} else {
    ENABLED_FEATURES = GA_FEATURES
}

android {
    resourcePrefix 'azure_communication_ui_calling_'
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField "String", "UI_SDK_VERSION", "\"" + call_library_version_name + "\""
        buildConfigField "String", "CALL_SDK_VERSION", "\"" + azure_calling_sdk_version + "\""
    }

    buildFeatures {
        buildConfig = true
        resValues = false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    for (String featureName : AVAILABLE_FEATURES) {
        // By default, use dummy implementations for the feature.
        def implFolder = "stubs"
        if (ENABLED_FEATURES.contains(featureName)) {
            // If the feature is in the FEATURES list, use the full implementation
            implFolder = "features"
        }

        // Add the SourceSets for the feature to the android project.
        android.sourceSets.main.java.srcDirs "src/${implFolder}/${featureName}/java"
        android.sourceSets.main.res.srcDirs "src/${implFolder}/${featureName}/res"
        android.sourceSets.main.assets.srcDirs "src/${implFolder}/${featureName}/assets"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xopt-in=kotlin.RequiresOptIn",
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
        ]
    }

    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging = true
            }
        }
        unitTests {
            returnDefaultValues = true
            all {
                failFast = true
            }
        }
    }

    ktlint {
        debug.set(false)
        verbose.set(true)
        android.set(true)
        outputToConsole.set(true)
        outputColorName.set("RED")
        enableExperimentalRules.set(false)
        disabledRules = ["import-ordering", "max-line-length", "parameter-list-wrapping"]
        reporters {
            reporter "checkstyle"
            reporter "plain"
        }
        filter {
            exclude({ it.file.absolutePath.contains("/generated/") })
        }
    }
    namespace 'com.azure.android.communication.ui.calling.implementation'

    if (!shouldByPassMavenPublish()) {
        publishing {
            multipleVariants('release') {
                withSourcesJar()
                withJavadocJar()
                includeBuildTypeValues('release')
            }
        }
    }
}


dependencies {
    implementation "androidx.core:core-ktx:$androidx_core_ktx_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_layout_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_fragment_ktx_version"

    api ("com.azure.android:azure-communication-calling:$azure_calling_sdk_version")
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_viewmodel_ktx_version"
    implementation "com.microsoft.fluentui:fluentui_others:$microsoft_fluent_ui_version"
    implementation "com.microsoft.fluentui:fluentui_drawer:$microsoft_fluent_ui_version"
    implementation "com.microsoft.fluentui:fluentui_persona:$microsoft_fluent_ui_version"
    implementation "com.microsoft.fluentui:fluentui_transients:$microsoft_fluent_ui_version"

    api 'com.jakewharton.threetenabp:threetenabp:1.4.4'

    testImplementation "androidx.arch.core:core-testing:$androidx_core_testing_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-inline:$mockito_inline_version"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockito_kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$jetbrains_kotlinx_coroutines_test_version"
    testImplementation('org.threeten:threetenbp:1.6.5') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_espresso_contrib_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_rules_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$jetbrains_kotlinx_coroutines_test_version"
}
