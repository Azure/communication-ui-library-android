apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.azure.android'
def CALL_WITH_CHAT_LIB_ARTIFACT_ID = 'azure-communication-ui-callwithchat'
def CALL_WITH_CHAT_LIB_VERSION = "$call_with_chat_library_version_name"
def CALL_WITH_CHAT_LIB_DESCRIPTION = "Azure Communication Call With Chat UI for Android."

Properties localProps = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    localProps.load(project.rootProject.file('local.properties').newDataInputStream())
}

publishing {
    repositories {
        maven {
            name = 'localDirectory'
            url = "file://${buildDir}/publish"
        }
    }

    publications {
        release(MavenPublication) {
            pom {
                name = "${LIB_GROUP_ID}:${CALL_WITH_CHAT_LIB_ARTIFACT_ID}"
                description = CALL_WITH_CHAT_LIB_DESCRIPTION
                url = "https://github.com/Azure/communication-ui-library-android"

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/Azure/communication-ui-library-android/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'Microsoft'
                        name = 'Microsoft'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Azure/communication-ui-library-android.git'
                    developerConnection = 'scm:git:ssh:github.com:Azure/communication-ui-library-android.git'
                    url = 'https://github.com/Azure/communication-ui-library-android'
                }
            }
            groupId LIB_GROUP_ID
            artifactId CALL_WITH_CHAT_LIB_ARTIFACT_ID
            version CALL_WITH_CHAT_LIB_VERSION

            afterEvaluate {
                from components.release
            }
        }

    }
}
