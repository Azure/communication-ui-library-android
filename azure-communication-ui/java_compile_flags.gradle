import java.util.regex.Pattern
import java.util.regex.Matcher


/**
 * Gradle Script for Managing Feature Flags in Java/Kotlin Projects
 *
 * This script provides tasks to list and toggle feature flags in Java and Kotlin source files.
 * Feature flags are identified by markers in the source code.
 *
 * Usage:
 * 1. Include this script in your project's build.gradle file.
 * 2. Use the provided Gradle tasks to manage your feature flags:
 *    - `listFlags`: Lists all the feature flags and their current status (Enabled/Disabled).
 *    - `toggleFlag`: Toggles a specified feature flag between enabled and disabled states.
 *
 * Command Line Usage:
 * - To list all feature flags:
 *   ./gradlew listFlags
 *
 * - To toggle a specific feature flag (replace 'YOUR_FLAG_NAME' with the actual flag name):
 *   ./gradlew toggleFlag -PflagName=YOUR_FLAG_NAME
 *
 * - To Contextualize a flag and it's code
 *   ./gradlew printFlag -PflagName=YOUR_FLAG_NAME
 */


// Task to list all feature flags and their status
def getFlagsStatus() {
    def flags = [:]
    def projectDir = project.projectDir
    def flagPattern = /\/\* <(.+?)>( \*)?/


    projectDir.traverse(type: groovy.io.FileType.FILES, nameFilter: ~/.*\.(java|kt)/) { file ->
        def content = file.text
        content.eachLine { line ->
            def matcher = (line =~ flagPattern)
            if (matcher.find()) {
                String flagName = matcher.group(1)
                // These are end tags, I.e. </FEATURE> = /FEATURE
                // We don't care when finding them.
                if (flagName.startsWith("/")) {
                    return
                }
                if (!flags.containsKey(flagName)) {
                    boolean isEnabled = content.contains("/* <${flagName}> */")
                    flags[flagName] = isEnabled ? "Enabled" : "Disabled"
                }
            }
        }
    }
    return flags
}

tasks.register('listFlags') {
    doLast {
        def flags = getFlagsStatus()

        println "\nFeature Flags (${flags.size()})"
        flags.each { key, value ->
            println "$key: $value"
        }
    }
    println "To Update:   gradlew toggleFlag -PflagName=YOUR_FLAG_NAME"
}

tasks.register('toggleFlag') {
    doFirst {
        if (!project.hasProperty('flagName')) {
            throw new GradleException("Flag name must be provided using the -PflagName=YOUR_FLAG_NAME argument.")
        }
    }

    doLast {
        String flagName = project.property('flagName')
        def flags = getFlagsStatus()
        boolean isEnabled = flags[flagName] == "Enabled"
        String startMarkerEnabled = "/* <${flagName}> */"
        String endMarkerEnabled = "/* </${flagName}> */"
        String startMarkerDisabled = "/* <${flagName}>"
        String endMarkerDisabled = "</${flagName}> */"

        FileTree files = fileTree(dir: 'src', includes: ['**/*.java', '**/*.kt'])

        files.each { File file ->
            String content = file.text
            boolean madeReplacement = false

            if (isEnabled) {
                if (content.contains(startMarkerEnabled) || content.contains(endMarkerEnabled)) {
                    println "Found enabled flag '${flagName}' in file: ${file}"
                    content = content.replaceAll(Pattern.quote(startMarkerEnabled), Matcher.quoteReplacement(startMarkerDisabled))
                    content = content.replaceAll(Pattern.quote(endMarkerEnabled), Matcher.quoteReplacement(endMarkerDisabled))
                    madeReplacement = true
                }
            } else {
                if (content.contains(startMarkerDisabled) || content.contains(endMarkerDisabled)) {
                    println "Found disabled flag '${flagName}' in file: ${file}"
                    content = content.replaceAll(Pattern.quote(startMarkerDisabled), Matcher.quoteReplacement(startMarkerEnabled))
                    content = content.replaceAll(Pattern.quote(endMarkerDisabled), Matcher.quoteReplacement(endMarkerEnabled))
                    madeReplacement = true
                }
            }

            if (madeReplacement) {
                file.text = content
                println "File updated: ${file}"
            } else {
                println "No changes made to file: ${file}"
            }
        }
    }
}

tasks.register('printFlag') {
    doFirst {
        if (!project.hasProperty('flagName')) {
            throw new GradleException("Flag name must be provided using the -PflagName=YOUR_FLAG_NAME argument.")
        }
    }

    doLast {
        String flagName = project.property('flagName')
        FileTree files = fileTree(dir: 'src', includes: ['**/*.java', '**/*.kt'])
        Pattern startPattern = Pattern.compile("/\\* <${flagName}(?::(\\d+))?>")
        Pattern endPattern = Pattern.compile("</${flagName}(?::(\\d+))?> \\*/")
        int defaultContextLines = 5 // Default number of contextual lines

        files.each { File file ->
            List<String> lines = file.readLines()
            boolean isReading = false
            int tagStart = -1
            int contextLinesBefore = defaultContextLines
            int contextLinesAfter = defaultContextLines

            lines.eachWithIndex { line, index ->
                Matcher startMatcher = startPattern.matcher(line)
                if (!isReading && startMatcher.find()) {
                    isReading = true
                    tagStart = index
                    contextLinesBefore = startMatcher.group(1) ? Integer.parseInt(startMatcher.group(1)) : defaultContextLines
                    // Calculate the start index for printing context before the flag
                    int contextStart = Math.max(0, index - contextLinesBefore)
                    println "File: ${file} - Flag: ${flagName}"
                    // Print context before the flag
                    lines.subList(contextStart, index).each { println "  ${it}" }
                }
                Matcher endMatcher = endPattern.matcher(line)
                if (isReading && endMatcher.find()) {
                    isReading = false
                    int tagEnd = index
                    contextLinesAfter = endMatcher.group(1) ? Integer.parseInt(endMatcher.group(1)) : defaultContextLines
                    // Calculate the end index for printing context after the flag
                    int contextEnd = Math.min(lines.size(), tagEnd + contextLinesAfter + 1)
                    // Print the flag content
                    lines.subList(tagStart + 1, tagEnd).each { println "+ ${it}" }
                    // Print context after the flag
                    lines.subList(tagEnd + 1, contextEnd).each { println "  ${it}" }
                    println "\n" // Separate blocks for readability
                }
            }
        }
    }
}
